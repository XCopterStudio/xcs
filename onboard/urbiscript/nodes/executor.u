class Executor : XObject {
    class State {
        var IDLE = "idle";
        var RUNNING = "running";
        var FREEZED = "freezed";
    };
    var inputs_ = [
        "code" => Pair.new(String, "URBISCRIPT"),
        "control" => Pair.new(String, "CONTROL"),
    ];
    var outputs_ = [
        "state" => Pair.new(String, "EXECUTION_STATE"),
        "error" => Pair.new(String, "EXECUTION_ERROR"),
        "output" => Pair.new(String, "EXECUTION_OUTPUT"),
    ];
    
    function init(lobby) {
        // Inputs
        var this.code = "";
        var this.control = "";
        // Outputs
        var this.state = State.IDLE;
        var this.error = nil;
        var this.output = nil;

        var this.state_ = 0; // NOTE: it's different from 'state' output port related to executed code
        var this.lobby_ = lobby;
        var this.tag = Tag.new();
        var this.job;
        var that = this;
        
        at(this.tag.leave?) {
            that.state = that.State.IDLE;
        };
        
        at(this.state_->changed?) {
            switch(that.state_) {
                case 2: // stopped
                    that.terminateExecution_();
            };
        };
        
        /*
         * Redefine lobby's echo so that we can use output
         * in user code.
         * 
         * NOTE: works only on top level (this == lobby_)
         * NOTE: doesn't work for channels (cout <<, etc.)
         */
        var myEcho = function(string) {
            that.output = string.asString() + "\n"; // TODO test on windows, define some built dependent constant
        };
        
        if(lobby_.hasLocalSlot("echo")) { // NOTE: this will modify global lobby once for all :-(
            lobby_.echo = myEcho;
        } else {
            var lobby_.echo = myEcho;
        };
        
        &control.notifyChange(function() {
            if(that.state_ != 1) {
                return;
            };
            if(that.control == "run") {
                that.state = that.State.RUNNING;
                that.job = disown({that.tag:{
                    try {
                        System.eval("{" + that.code + "}", that.lobby_); // we run the code in a separate scope
                    } catch(var e) {
                        that.error = e.asString();
                    };
                }}); // run in background                
            } else if(that.control == "freeze") {
                that.tag.freeze();
                that.state = that.State.FREEZED;
            } else if(that.control == "unfreeze") {
                that.state = that.State.RUNNING;
                that.tag.unfreeze();
            } else if(that.control == "stop") {
                that.terminateExecution_();
            }
        });
    }|;
    
    function terminateExecution_() {
        tag.stop();
        if(tag.frozen) { // tag.stop() does leave the code freezed
            tag.unfreeze();
        };
        if(job) {
            job.terminate();
        };
        // NOTE: state will be set in event handler for tag leave        
    };
    
}|;
