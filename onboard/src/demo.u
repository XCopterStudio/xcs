/*
 * Class definitions (TODO put in another file)
 */
class Adapter {

    var _socket;
    var output;

    function init() {
        output = Object.clone();
    };

    function start(server, port) {
        _socket = Socket.new();
        _socket.connect(server, port);
        at (_socket.received?(var data)) {
            echo("Received: " + data);
            output = data;
        };
        clog << "Started adapter %s:%s." % [server, port];
    };
    
    function send(data) {
        assert(_socket != nil);
        _socket.write(data);
    };
}|;

class FlashSender {

    function init(adapter, UJson) {
        var this.adapter = adapter;
        var this.ujson = UJson;        
    }|;

    function send(message) {
        var data = [ => ];
        data["type"] = "flash";
        data["data"] = message;
        this.adapter.send(this.ujson.encode(data) + "\n");
    }|;
}|;


class SemanticSender {

    function init(adapter, outputs, UJson) {
        for(var output: outputs) {
            setSlot(output.first, nil);
            var slot = getSlot(output.first);
            var that = this;

            slot.notifyChange(function() {
                // simple JSONÂ encoder -- no escaping!
                var data = [ => ];
                data["type"] = "data";
                data["data"] = [ output.first => that.getSlotValue(output.first) ];
                adapter.send(UJson.encode(data) + "\n");
            });
        };
    }|;
}|;

class SemanticReceiver {
    function init(adapter, inputs, UJson) { // parseJson as arg, lookup is wierd in this case (package/import?)
        for(var input: inputs) setSlot(input.first, nil);

        var that = this;
        at(adapter.output->changed?) {        
            var rawData = UJson.decode(adapter.output);
            var data = rawData["data"];
            for(var input: inputs) {
                var name = input.first;
                if(name in data) {
                    echo("updating: " + name);
                    that.updateSlot(name, data[name]);
                }
            };
        };
    };
}|;

/*
 * Dataflow graph
 */

// connect to onboard
var adapter = Adapter.new();
//adapter.start("192.168.0.99", 1234);
adapter.start("127.0.0.1", 1234);

// create graph nodes
var xci = UXci.new("xci-dodo");
var semanticSender = SemanticSender.new(adapter, [
    "altitude" => "altitude",
    "phi" => "phi",
    "psi" => "psi",
    "theta" => "theta",
    "alive" => "alive",
], UJson);

var flyParamReceiver = SemanticReceiver.new(adapter, [
    "roll" => "roll",
    "pitch" => "pitch",
    "yaw" => "yaw",
    "gaz" => "gaz",
    "command" => "command"
], UJson);

var flashSender = FlashSender.new(adapter, UJson);

flashSender.send("Created graph nodes.");

// connect graph nodes
xci.&altitude >> semanticSender.&altitude;
xci.&phi >> semanticSender.&phi;
xci.&psi >> semanticSender.&psi;
xci.&theta >> semanticSender.&theta;
xci.&alive >> semanticSender.&alive;

flyParamReceiver.&roll >> xci.&roll;
flyParamReceiver.&pitch >> xci.&pitch;
flyParamReceiver.&yaw >> xci.&yaw;
flyParamReceiver.&gaz >> xci.&gaz;
flyParamReceiver.&command >> xci.&command;

flashSender.send("Connected graph nodes.");

// enable/start graph nodes

/*
xci.xciInit();
xci.doCommand("TakeOff");
sleep(3000ms);
xci.flyParam(0, 0, 0.8, 0);
sleep(4000ms);
xci.flyParam(0, 0, 0, 0);
sleep(3500ms);
xci.doCommand("Land");
*/
